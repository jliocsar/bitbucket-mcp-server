{ $ } from bun
fs from node:fs

* as tar from tar

// Build configuration
targets :=
  . 'bun-linux-x64'
  . 'bun-linux-arm64'
  . 'bun-darwin-x64'
  . 'bun-darwin-arm64'
  . 'bun-windows-x64'
outputDir := './dist'

// Transpile all Civet source files to JS
await Promise.all [
  $`civet --js -c src/*.civet -o ./dist/.js`
  $`civet --js -c src/tools/*.civet -o ./dist/tools/.js`
]

// Create the output directory if it doesn't exist
unless fs.existsSync outputDir {
  fs.mkdirSync outputDir
}

// Build the binary for each target
await Promise.all targets.map (target) -> {
  console.info `Building for ${target}`

  // Remove the `bun-` prefix from the output filename
  suffix := target.replace 'bun-', ''
  outfile := `${outputDir}/bitbucket-mcp-server-${suffix}`

  // Build the binary
  try {
    await $`bun build \
      --compile \
      --minify \
      --sourcemap \
      --target=${target} \
      ./dist/index.js \
      --outfile ${outfile}`

    outputFileName := outfile.includes('windows') ? `${outfile}.exe` : outfile

    // Create a tarball of the binary
    await tar.create {
      +gzip
      file: `${outfile}.tar.gz`
    }, [outputFileName]

    // Remove the binary
    fs.unlinkSync outputFileName

    console.info `Built for ${target}`
  } catch error {
    console.error `Error building for ${target}: ${error}`
  }
}